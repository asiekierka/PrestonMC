buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
	maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.net.minecrell:licenser:0.3"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'net.minecrell.licenser'

version = "0.4.0"
group = "pl.asie.preston" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Preston"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.2.2654"
    runDir = "run"
    
    mappings = "snapshot_20180405"

    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

repositories {
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'BuildCraft'
        url 'https://mod-buildcraft.com/maven'
    }
    maven {
        name 'IC2'
        url 'http://maven.ic2.player.to'
    }
    maven {
        url "http://tehnut.info/maven"
    }
    maven {
        url "http://repo.elytradev.com"
    }
    maven {
        url "http://maven.blamejared.com/"
    }
    ivy {
        name 'asie dependency mirror'
        artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
    }
}

dependencies {
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.6.457"
    deobfCompile "com.mod-buildcraft:buildcraft-api:7.99.15"
    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.159"
    deobfCompile "net.industrial-craft:industrialcraft-2:2.8.73-ex112"
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.23-B38_1.12"
    deobfCompile "com.elytradev:mirage:2.0.3-rc3-SNAPSHOT"
    deobfCompile "com.elytradev:probedataproviderapi:MC1.12_ver1.1.1"
    deobfCompile name: "ProjectE", version: "1.12-PE1.2.0"
}

license {
    sourceSets = [project.sourceSets.main]
    header = project.file("docs/licenses/LICENSE")
    ignoreFailures = true
    exclude '**/*.lang'
    exclude '**/*.info'
    exclude '**/*.mcmeta'

    matching('**/pl/asie/preston/api/**') {
        header = project.file("docs/licenses/LICENSE-API")
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
